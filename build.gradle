import com.android.build.gradle.internal.SdkHandler

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.3'
    classpath 'org.codehaus.groovy:groovy-android-gradle-plugin:1.0.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.android'

repositories {
  jcenter()
}

android {
  compileSdkVersion 24
  buildToolsVersion '24.0.1'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 24

    jackOptions {
      enabled true
    }
  }

  signingConfigs {
    release {
      storeFile file(System.getenv("KEYSTORE") ?: debug.storeFile)
      storePassword System.getenv("KEYSTORE_PASSWORD") ?: 'foo'
      keyAlias System.getenv("KEY_ALIAS") ?: 'foo'
      keyPassword System.getenv("KEY_PASSWORD") ?: 'foo'
    }
  }

  buildTypes {
    debug {
    }
    release {
      signingConfig signingConfigs.release
    }
  }

  // must use java 8 in order to include the dx.jar since it was compiled with java 8.
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

def sdkHandler = new SdkHandler(project, null)

dependencies {
  compile files("$sdkHandler.sdkFolder/build-tools/$android.buildToolsVersion/lib/dx.jar")
  compile 'org.codehaus.groovy:groovy:2.4.7:grooid'
}
